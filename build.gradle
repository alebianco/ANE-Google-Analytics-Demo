/*
 * Project: ANELytics-Demo
 *
 * Author:  Alessandro Bianco
 * Website: http://alessandrobianco.eu
 * Twitter: @alebianco
 * Created: 18/12/14 10.15
 */

 buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath group: 'org.gradlefx', name: 'gradlefx', version: '1.1' // development release
    }
    project.properties.ext.setProperty('os', System.properties['os.name'].toLowerCase()[0..2])
}

apply plugin: 'gradlefx'

type = 'mobile'

mainClass = 'Main.as'

air {
	storepass = project.storepass

	locales	= ['en_US']

	additionalCompilerOptions = [
	    '-swf-version=26',
	    '-target-player=15.0',
	    '-static-link-runtime-shared-libraries=true',
	    '-define=CONFIG::debug,true',
	]

	includeFileTrees = [
		fileTree(dir: 'src/main/resources/')
	]
}

airMobile.extensionDir = 'src/main/libs'
airMobile.platform = project.platform
if (project.hasProperty('targetDevice'))
    airMobile.targetDevice = project.targetDevice
if ('ios'.equals(project.platform)) {
    airMobile.target = 'ipa-debug-interpreter'
    airMobile.simulatorTarget = 'ipa-debug-interpreter-simulator'
    airMobile.provisioningProfile = 'ANELytics-Demo.mobileprovision'
    if ('true'.equals(project.simulator)) {
        airMobile.simulatorPlatformSdk = '/Applications/Xcode.app/Contents/Developer/Platforms/iPhoneSimulator.platform/Developer/SDKs/iPhoneSimulator.sdk/'
    }
} else if ('android'.equals(project.platform)) {
    airMobile.extensionDir = 'src/main/libs'
    airMobile.platform = 'android'
    airMobile.target = 'apk-debug'
    airMobile.simulatorTarget = 'apk-emulator'
}

repositories {
    ivy {
        name 'Apache'
        artifactPattern 'http://archive.apache.org/dist/flex/[revision]/binaries/[module]-[revision]-bin.[ext]'
    }

    ivy {
        name 'Adobe Air SDK'
        artifactPattern 'http://download.macromedia.com/air/win/download/[revision]/[module].[ext]'
    }
}

dependencies {
    flexSDK group: 'org.apache', name: 'apache-flex-sdk', version: '4.13.0', ext: 'mac'.equals(os) ? 'tar.gz' : 'zip'
    airSDK group: 'com.adobe', name: 'AdobeAIRSDK', version: '15.0', ext: 'zip'

    merged files("src/main/libs")
}

sdkAutoInstall {
    showPrompts = false
}

['install', 'uninstall', 'launch'].each { def targetName ->
    tasks.create(name: "$targetName") {
        dependsOn(":$targetName" + ('true'.equals(project.simulator) ? 'Simulator' : '') + 'Mobile')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
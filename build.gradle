/*
 * Project: ANE-Google-Analytics
 *
 * Author:  Alessandro Bianco
 * Website: http://alessandrobianco.eu
 * Twitter: @alebianco
 * Created: 18/12/14 10.15
 */

 buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath group: 'org.gradlefx', name: 'gradlefx', version: '1.1' // development release
    }

    project.properties.ext.setProperty('os', System.properties['os.name'].toLowerCase()[0..2])
    project.properties.ext.setProperty('sim', project.hasProperty('sim'))
    project.properties.ext.setProperty('platform', project.hasProperty('ios') ? 'ios' : 'android')

}

apply plugin: 'gradlefx'

type = 'mobile'

mainClass = 'Main.as'

air {

	locales	= ['en_US']

	additionalCompilerOptions = [
	    '-swf-version=26',
	    '-target-player=15.0',
	    '-verbose-stacktraces=true',
	    '-load-config=air-config.xml',
	    '-static-link-runtime-shared-libraries=true',
	    '-define=CONFIG::debug,true',
	]

	includeFileTrees = [
		fileTree(dir: 'src/main/resources/', include: 'icons/*')
	]
}

airMobile {
    extensionDir = 'src/main/libs'
    platform = platform
}

if ('ios'.equals(platform)) {
    airMobile {
        target = 'ipa-debug-interpreter'
        simulatorTarget = 'ipa-debug-interpreter-simulator'
        provisioningProfile = 'ANELytics.mobileprofile'
        if (!sim) targetDevice = 22
        if (sim) simulatorPlatformSdk = '/Applications/Xcode.app/Contents/Developer/Platforms/iPhoneSimulator.platform/Developer/SDKs/iPhoneSimulator.sdk/'
    }
} else {
    airMobile {
        extensionDir = 'src/main/libs'
        platform = 'android'
        target = 'apk-debug'
        simulatorTarget = 'apk-emulator'
    }
}

repositories {
    ivy {
        name 'Apache'
        artifactPattern 'http://archive.apache.org/dist/flex/[revision]/binaries/[module]-[revision]-bin.[ext]'
    }

    ivy {
        name 'Adobe Air SDK'
        artifactPattern 'http://download.macromedia.com/air/win/download/[revision]/[module].[ext]'
    }
}

dependencies {
    flexSDK group: 'org.apache', name: 'apache-flex-sdk', version: '4.13.0', ext: 'mac'.equals(os) ? 'tar.gz' : 'zip'
    airSDK group: 'com.adobe', name: 'AdobeAIRSDK', version: '15.0', ext: 'zip'

    merged files("src/main/libs")
}

sdkAutoInstall {
    showPrompts = false
}

task install {
    dependsOn(':install' + (project.sim ? 'Simulator' : '') + 'Mobile')
}
task uninstall {
    dependsOn(':uninstall' + (project.sim ? 'Simulator' : '') + 'Mobile')
}
task launch {
    dependsOn(':launch' + (project.sim ? 'Simulator' : '') + 'Mobile')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}